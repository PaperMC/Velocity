plugins {
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
}

apply plugin: 'org.cadixdev.licenser'
apply from: '../gradle/checkstyle.gradle'
apply from: '../gradle/publish.gradle'
apply from: '../gradle/errorprone.gradle'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

license {
    header = project.file('HEADER.txt')
}

dependencies {
    api 'com.google.code.gson:gson:2.8.6'
    api "com.google.guava:guava:${guavaVersion}"

    api "net.kyori:adventure-api:${adventureVersion}"
    api "net.kyori:adventure-text-serializer-gson:${adventureVersion}"
    api "net.kyori:adventure-text-serializer-legacy:${adventureVersion}"
    api "net.kyori:adventure-text-serializer-plain:${adventureVersion}"
    api("net.kyori:coffee:1.0.0-SNAPSHOT")

    api "org.slf4j:slf4j-api:${slf4jVersion}"
    api 'com.google.inject:guice:5.0.1'
    api "org.checkerframework:checker-qual:${checkerFrameworkVersion}"
    api 'com.velocitypowered:velocity-brigadier:1.0.0-SNAPSHOT'

    api "org.spongepowered:configurate-hocon:${configurateVersion}"
    api "org.spongepowered:configurate-yaml:${configurateVersion}"
    api "org.spongepowered:configurate-gson:${configurateVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.source = '8'
    options.links(
            'http://www.slf4j.org/apidocs/',
            'https://google.github.io/guava/releases/30.0-jre/api/docs/',
            'https://google.github.io/guice/api-docs/4.2/javadoc/',
            'https://docs.oracle.com/javase/8/docs/api/',
            'https://jd.adventure.kyori.net/api/4.7.0/'
    )

    // Disable the crazy super-strict doclint tool in Java 8
    options.addStringOption('Xdoclint:none', '-quiet')

    // Mark sources as Java 8 source compatible
    options.source = '8'

    // Remove 'undefined' from seach paths when generating javadoc for a non-modular project (JDK-8215291)
    if (JavaVersion.current() >= JavaVersion.VERSION_1_9 && JavaVersion.current() < JavaVersion.VERSION_12) {
        options.addBooleanOption('-no-module-directories', true)
    }
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}
