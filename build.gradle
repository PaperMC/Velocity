buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
    }
}

plugins {
    id 'java'
}

allprojects {
    group 'com.velocitypowered'
    version '1.0.10-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        // dependency versions
        textVersion = '3.0.3'
        junitVersion = '5.3.0-M1'
        slf4jVersion = '1.7.25'
        log4jVersion = '2.11.2'
        nettyVersion = '4.1.49.Final'
        guavaVersion = '25.1-jre'
        checkerFrameworkVersion = '2.7.0'

        getCurrentShortRevision = {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    executable = "git"
                    args = ["rev-parse", "HEAD"]
                    standardOutput = os
                }
                return os.toString().trim().substring(0, 8)
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        
        // for kyoripowered dependencies
        maven {
            url 'https://oss.sonatype.org/content/groups/public/'
        }
        
        // Brigadier
        maven {
            url "https://libraries.minecraft.net"
        }
    }

    test {
        reports {
            junitXml.enabled = true
        }
    }
}
