buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
    }
}

plugins {
    id 'java'
}

allprojects {
    group 'com.velocitypowered'
    version '1.1.0-SNAPSHOT'

    ext {
        // dependency versions
        textVersion = '3.0.4'
        adventureVersion = '4.0.0-SNAPSHOT'
        junitVersion = '5.3.0-M1'
        slf4jVersion = '1.7.25'
        log4jVersion = '2.13.3'
        nettyVersion = '4.1.51.Final'
        guavaVersion = '25.1-jre'
        checkerFrameworkVersion = '2.7.0'
        configurateVersion = '3.6'

        getCurrentShortRevision = {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    executable = "git"
                    args = ["rev-parse", "HEAD"]
                    standardOutput = os
                }
                return os.toString().trim().substring(0, 8)
            }
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    repositories {
        mavenLocal()
        mavenCentral()

        // for kyoripowered dependencies
        maven {
            url 'https://oss.sonatype.org/content/groups/public/'
        }

        // Brigadier
        maven {
            url "https://libraries.minecraft.net"
        }

        // Configurate
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }

    test {
        reports {
            junitXml.enabled = true
        }
    }
}
