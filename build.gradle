plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
    id 'org.cadixdev.licenser' version '0.6.1' apply false
}

allprojects {
    group 'com.velocitypowered'
    version '3.1.2-SNAPSHOT'

    ext {
        // dependency versions
        adventureVersion = '4.11.0'
        junitVersion = '5.9.0'
        slf4jVersion = '2.0.1'
        log4jVersion = '2.19.0'
        nettyVersion = '4.1.82.Final'
        guavaVersion = '31.1-jre'
        checkerFrameworkVersion = '3.25.0'
        configurateVersion = '4.1.2'

        getCurrentShortRevision = {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    executable = "git"
                    args = ["rev-parse", "HEAD"]
                    standardOutput = os
                }
                return os.toString().trim().substring(0, 8)
            }
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    repositories {
        mavenLocal()
        mavenCentral()

        // for kyoripowered dependencies
        maven {
            url 'https://oss.sonatype.org/content/groups/public/'
        }

        // Velocity repo
        maven {
            url "https://nexus.velocitypowered.com/repository/maven-public/"
        }
    }

    test {
        reports {
            junitXml.required = true
        }
    }
}
