plugins {
    id 'java'
}

allprojects {
    group 'com.velocitypowered'
    version '1.0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        // dependency versions
        junitVersion = '5.3.0-M1'
        slf4jVersion = '1.7.25'
        log4jVersion = '2.11.0'
        nettyVersion = '4.1.28.Final'
        guavaVersion = '25.1-jre'

        getCurrentBranchName = {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    executable = "git"
                    args = ["rev-parse", "--abbrev-ref", "HEAD"]
                    standardOutput = os
                }
                return os.toString()
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/groups/public/'
        }
    }

    test {
        reports {
            junitXml.enabled = true
        }
    }
}
