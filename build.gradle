plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0' apply false
    id 'org.cadixdev.licenser' version '0.6.1' apply false
}

allprojects {
    group 'com.velocitypowered'
    version '3.1.2-SNAPSHOT'

    ext {
        // dependency versions
        adventureVersion = '4.10.1'
        junitVersion = '5.7.0'
        slf4jVersion = '1.7.30'
        log4jVersion = '2.17.1'
        nettyVersion = '4.1.77.Final'
        guavaVersion = '25.1-jre'
        checkerFrameworkVersion = '3.6.1'
        configurateVersion = '3.7.2'

        getCurrentShortRevision = {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    executable = "git"
                    args = ["rev-parse", "HEAD"]
                    standardOutput = os
                }
                return os.toString().trim().substring(0, 8)
            }
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    repositories {
        mavenLocal()
        mavenCentral()

        // for kyoripowered dependencies
        maven {
            url 'https://oss.sonatype.org/content/groups/public/'
        }

        // Velocity repo
        maven {
            url "https://nexus.velocitypowered.com/repository/maven-public/"
        }
    }

    test {
        reports {
            junitXml.required = true
        }
    }
}
